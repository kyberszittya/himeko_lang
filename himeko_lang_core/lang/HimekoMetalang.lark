// hi_*: abbreviation for Himeko mandatory language elements
start: "[" hi_meta "]"  hi_body

?hi_meta: hi_metaelement

hi_metaelement: element_name [(hi_include)+]
// Metadata elements
hi_include: "import" ":" string

hi_body: (hi_node | hi_element_field)+

hi_node: hi_element_signature (hi_fields)? "{" (hi_node | hi_edge | hi_element_field)* "}"
hi_edge: hi_element_signature (hi_fields)? ":" (hi_edge_element) ("," hi_edge_element)*
hi_fields: (hi_element_field)+
// Hypergraph elements
hi_element_signature: element_name hi_templating?
hi_templating: "^" element_reference
hi_element_field: element_name ("<" (element_type) ">")? (hi_element_value|vector_field)?";"
element_type: (EL_ATOMIC|"ref")

vector_field: "[" hi_element_value ("," hi_element_value)* "]"
hi_element_value: (SIGNED_NUMBER | STRING | element_reference)
// Edge elements
hi_edge_element: (RELATION_DIRECTION|parameter_brackets) element_reference
// Element name
element_name: CNAME
element_reference: "@>" STRING parameter_brackets?
parameter_brackets: "(" hi_element_value ("," hi_element_value)* ")"
// Datatype aliasi
string: ESCAPED_STRING
// Direction
RELATION_DIRECTION: "->"|"<-"|"--"|"<>"
// Atomic field types
EL_ATOMIC: "int"|"string"|"real"


%import common.SIGNED_NUMBER
%import common.NUMBER
%import common.CNAME
%import python (NAME, STRING, DEC_NUMBER)
%import common.ESCAPED_STRING
%import common.WS

%ignore WS